package Picking
import UnitObjEditing
import MapStuff
import Crusader
import Technician
import Mage
import Specialist
import Hero
import Fx
import Terrain
import Intermission
import Queue
import ClosureTimers
import ItemObjEditing
import Players
public int PICK_SPELL_ID

LinkedList<Pickable> pickables
LinkedList<player> picked


function getHeroDescription(int id) returns string
	switch id
		case 0 //Crusader
			return "Crusader is the main supportive hero of the game, who is also able to withstand tons of damage. He can be vital for surviving."
		case 1 //Technician
			return "Technician is the most tactical hero. You can build your own battlefield, with huge explosives and fire."
		case 2 //Mage
			return "\"Mage is truly epic.\" She stands in the middle of the battlefield, exploding enemies. Be careful though, if creeps manage to get near her, she dont have long left."
		case 3 //Specialist
			return "Specialist is great at manipulating enemies, and reforming their hordes. Use him on side of a mage for maximal efficenty!"
		default
			return "The Ghost Hero is the most powerful of all, truly,yea truly powerful. You may pick him quick, and pick him now!"

	
class Pickable
	Hero h
	Fx platform
	int id
	trigger castTrigger

	construct(int ord, Hero hero, Fx fx)
		h = hero
		platform = fx
		id = ord
		
		//This trigger is only created, because i wanted to test PICK_SPELL_ID in this package instead of doing it in Hero.wurst
		castTrigger = CreateTrigger()
		castTrigger.registerUnitEvent(h.actor, EVENT_UNIT_SELL)
		castTrigger.addAction(function onSell)
		
		//Setting some properties of the hero actor:
		h.actor.setPropWindow(0.)
		h.sleeping = true
		h.actor.addAbility(PICK_SPELL_ID)
		ModifyHeroSkillPoints(h.actor, bj_MODIFYMETHOD_SET, 0)
		h.actor.setPathing(false)
		hero.actor.setOwner(DUMMY_PLAYER, true)
		hero.owner = DUMMY_PLAYER
		hero.weapon.refreshWeapon(hero.actor)
		if hero instanceof Mage
			AddUnitToStock(hero.actor, UNAVAILABLE_ID, 0, 1)
		else
			AddUnitToStock(hero.actor, PICKDUMMY_ID, 1, 1)
		
		
		
	//Called when the pick is casted. Picks the hero.
	function pick(player p)
		h.actor.setOwner(p,true)
		h.owner = p
		h.sleeping = false
		h.actor.setPropWindow(GetUnitDefaultPropWindow(h.actor))
		h.actor.removeAbility(PICK_SPELL_ID)
		ModifyHeroSkillPoints(h.actor, bj_MODIFYMETHOD_SET, 1)
		h.actor.setPathing(true)
		ResetToGameCameraForPlayer(p, 1.3)
		pDatas[p.getId()].pickdummy.remove()
		pDatas[p.getId()].pickdummy = null
		picked.remove(p)
		if GetLocalPlayer() == h.owner
			ResetTerrainFog()
		destroy this

				
	static function onSell()
		if GetSpellAbilityId() == PICK_SPELL_ID
			var u = GetSoldUnit()
			var p = u.getOwner()
			var h = GetTriggerUnit().getEntity() castTo Hero
			
			
			for pi in pickables
				if h == pi.h
					pi.pick(p)
			u.remove()

	ondestroy
		pickables.remove(this)
		destroy platform
		castTrigger.destr()
		checkIfStart()
		h.actor..removeAbility('Ane2')..removeAbility('Asud')
	
function createHero(int id, vec3 pos) returns Hero
	Hero h = null
	if id == 0
		h = new Crusader(pos,Player(8) )
	else if id == 1
		h = new Technician(pos,Player(8) )
	else if id == 2
		h = new Mage(pos,Player(8) )
	else if id == 3
		h = new Specialist(pos,Player(8) )
	return h

public function createPickabes()
	pickables = new LinkedList<Pickable> ()
	for i = 0 to heroCount-1
		var a = angle(i*2*PI/heroCount)
		var offset = STATUE_POS.polarOffset(a,200)
		var z = getTerrainZ(offset.x,offset.y)
		let fx = new Fx(offset.withZ(2000),a, "buildings\\other\\MagicVault\\MagicVault.mdl")
		fx.setZ(2000-z)
		pickables.add(new Pickable(i,createHero(i,offset.withZ(2200)),fx))
			
	for p in allPlayers
		p.pickdummy = createUnit(p.p, PICKDUMMY_ID, STATUE_POS, angle(0))	
			//Todo playernumber
	camerasetup pick_cam = gg_cam_Pick_Camera
	CameraSetupSetDestPosition(pick_cam, STATUE_POS.x,STATUE_POS.y,0.)
	doPeriodically(ANIMATION_PERIOD, (CallbackPeriodic cp) -> begin
		var i = 0
		for p in allPlayers
			if p.pickdummy != null
				i++
				if p.p == GetLocalPlayer()
					CameraSetupApplyForceDuration(pick_cam, true,0.)
		if i == 0
			destroy cp
	end)
	
	
boolean started = false

function checkIfStart()
	if not started and picked.getSize() == 0
		startIntermission(10, "You have 20 seconds till the first attack..")
		started = true
		destroy picked
		for pi in pickables
			destroy pi.h
			//**WARNING! THIS IS UGLY**//
			doAfter(ANIMATION_PERIOD, ()->destroy pi)
	
int PICKDUMMY_ID
int UNAVAILABLE_ID

public function init_Picking(boolean ct)
	if not ct
		picked = new LinkedList<player>()
		for pd in allPlayers
			picked.add(pd.p)

	PICKDUMMY_ID = UNIT_ID_GEN.next()
	UNAVAILABLE_ID = UNIT_ID_GEN.next()
	if ct
		let def = new UnitDefinition(PICKDUMMY_ID, 'hfoo')
		..setNormalAbilities("Agho")..setSightRadiusDay(0)..setSightRadiusNight(0)..setModelFile("")
		..setName("Pick Hero")..setGoldCost(0)..setFoodCost(0)..setTooltipBasic("Pick Hero")..setTooltipExtended("")
		..setIconGameInterface("ReplaceableTextures\\CommandButtons\\BTNPickUpItem.blp")
		
		let def2 = new UnitDefinition(UNAVAILABLE_ID, 'hfoo')
		..setNormalAbilities("Agho")..setSightRadiusDay(0)..setSightRadiusNight(0)..setModelFile("")
		..setName("Hero unavailable")..setGoldCost(0)..setFoodCost(0)..setTooltipBasic("Hero unavailable")..setTooltipExtended("This hero is not finished yet and only selectable by creators")
		..setStockStartDelay(10000)..setStockReplenishInterval(10000)
		..setIconGameInterface("ReplaceableTextures\\CommandButtons\\BTNPickUpItem.blp")
		
