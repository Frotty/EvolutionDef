package EventManager
import DamageDetection
import Entity
import OnUnitEnterLeave
import DamageMod
import RegisterEvents
import initlater AiManager

public enum UnitEvent
	DAMAGED
	DAMAGEINFLICTED
	CHANGESTATE
	
public function getInflictedDamage() returns real
	return getDamage()
	
unit temptarget = null
public function getDamageTarget() returns unit
	return temptarget

public interface UnitEventInterface
	function onEvent(UnitEvent evt)

public class EventManager
	static UnitEventInterface array objects
	private static State temp_State = null
	
	static function getNewState() returns State
		return temp_State
	
	static function registerUnit(Entity e, UnitEventInterface onEvent)
		objects[e castTo int] = onEvent
		
	static function unregisterUnit(Entity e)
		objects[e castTo int] = null
		
	static function fireEventFor(Entity e, UnitEvent evt)
		let index = e castTo int
		if objects[index] != null
			objects[index].onEvent(evt)
		
	static function fireEventFor(Entity e, UnitEvent evt, State state)
		temp_State = state
		let index = e castTo int
		if objects[index] != null
			objects[index].onEvent(evt)
		temp_State = null
		
	static function onDamageEvent() returns boolean
		let damaged = GetTriggerUnit()
		let source = GetEventDamageSource()
		fireEventFor(damaged.getEntity(), UnitEvent.DAMAGED)
		temptarget = damaged
		fireEventFor(source.getEntity(), UnitEvent.DAMAGEINFLICTED)
		temptarget = null
		return false
		
		
	static function onCustomEvent()
	
init
	addOnDamageFunc(Filter(function EventManager.onDamageEvent))
	registerPlayerUnitEvent(EVENT_PLAYER_UNIT_DEATH, () -> EventManager.unregisterUnit(GetDyingUnit().getEntity()))
