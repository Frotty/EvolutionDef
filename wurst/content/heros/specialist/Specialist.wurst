package Specialist

import MapStuff
import ChannelAbilityPreset
import AbilityObjEditing
import Hero
import ClosureTimers
import Terrain
import Feed
import Spear
import Cage
import Trojan

int SPECIALIST_ID


public class Specialist extends Hero
	Cage cage
	int trojanDamage = 0

	construct(vec3 pos, player owner )
		super(pos, owner, SPECIALIST_ID)
		
	override function onCast(int spellId, int lvl)
	
	override function onUnitCast(int spellId, unit target, int lvl)
		if spellId == CAGE_SPELL_ID
			cage = new Cage(getPos3(), this, lvl)
		else if spellId == CAGE_SECONDARY_SPELL_ID
			cage.addNode(getPos3())
		else if spellId == SPEAR_SPELL_ID
			var xyAngle = getPos3().angleTo2d(target.getPos())
			if target == actor // Avoiding agnle being 0 when cast on self (near hero)
				xyAngle = actor.getFacingAngle()
			new Spear(getPos3(), owner, xyAngle ,lvl)
		else if spellId == FEED_SPELL_ID
			var xyAngle = getPos3().angleTo2d(target.getPos())
			if target == actor // Avoiding agnle being 0 when cast on self (near hero)
				xyAngle = actor.getFacingAngle()
			new FoodProjectile( getPos3(), owner, getPos2().distToVec(target.getPos()) + 10., xyAngle, lvl)
		else if spellId == TROJAN_SPELL_ID
			new TrojanBuff(actor, lvl)

	override function onPointCast(int spellId, vec2 target, int lvl)
		if spellId == FEED_SPELL_ID
			new FoodProjectile( getPos3(), owner, getPos2().distToVec(target), getPos2().angleTo(target), lvl)
		if spellId == SPEAR_SPELL_ID
			new Spear(getPos3()-vec3(0,0,getTZ()), owner,  getPos2().angleTo(target) ,lvl)


public function init_Specialist(boolean compileTime)
	SPECIALIST_ID = HERO_ID_GEN.next()
	// generate valid id
	init_Feed(compileTime)
	init_Spear(compileTime)
	init_Trojan(compileTime)
	//SAUSAGE_SPEAR_SPELL_ID = ABIL_ID_GEN.next()
	init_Cage(compileTime)
	
	if compileTime
		// Generate def if compieltime
		
		let preset = new HeroPreset(SPECIALIST_ID, 'Edem', "Specialist")
		preset..addProperName("Francis")
			..setModelFile("Models\\DarkSoldier_Hero.mdl")
			..setIconGameInterface("ReplaceableTextures\\CommandButtons\\BTNDarkSoldier.blp")
			..addHeroAbility(FEED_SPELL_ID)
			..addHeroAbility(SPEAR_SPELL_ID)
			..addHeroAbility(TROJAN_SPELL_ID)
			..addHeroAbility(CAGE_SPELL_ID)
			..setStartingAgility(21)..setAgilityPerLevel(1.8)
			..setStartingIntelligence(16)..setIntelligencePerLevel(2)
			..setStartingStrength(21)..setStrengthPerLevel(2)
		// Build at the end
			..buildHero()
