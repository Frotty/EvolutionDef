package RapidShoot
import Projectile
import Hero
import ClosureTimers
import MapStuff
import AbilityTooltipGenerator
import ChannelAbilityPreset
import CollisionDetection
import Creep

public int RAPID_SHOOT_ID
public int DISABLE_ATTACK_ID
constant RAPID_SHOOT_MAXLVL = 4
constant RAPID_SHOOT_NAME = "Rapid Shoot"

function getRapidShootDamage(int lvl) returns real
	return 10 + 2. * lvl

function getRapidShootDuration(int lvl) returns int
	return min(4, 2 + lvl)

public function Hero.startRapidShoot(real dur, real interval, int lvl)
	this.actor..setPropWindow(0)..setTurnSpeed(0.1)
	..addAbility(DISABLE_ATTACK_ID)
	
	doPeriodicallyTimed(interval, dur, (CallbackCounted cb) -> begin
		let p = new Projectile(this.getPos3() + vec3(0,0,45), this.owner, this.actor.getFacingAngle(), "Models\\BeamMissile.mdx")
		..setTimed(2)..setSpeed(500)..setFixedFlyHeight(45)
		p.fx..setScale(0.5)..recolor(colorA(255, 255, 125, 125))
		this.actor.setAnimation("Spell")
		p.fx.getDummy().registerOnHitCallback(65, true, (unit hit, unit hitter) -> begin
			if hitter == p.fx.getDummy() and hit.isAliveTrick() and hit.getEntity() instanceof Creep
				hitter.damageTarget(hit,getRapidShootDamage(lvl))
				hitter.removeOnHitCallback()
				destroy p
		end)
		if cb.isLast()
			this.actor.setPropWindow(80)
			this.actor.setTurnSpeed(0.6)
			this.actor.removeAbility(DISABLE_ATTACK_ID)
			
	end)
	
public function init_RapidShoot(boolean c)
	RAPID_SHOOT_ID = ABIL_ID_GEN.next()
	DISABLE_ATTACK_ID = ABIL_ID_GEN.next()
	if c
		let def = new ChannelAbilityPreset(RAPID_SHOOT_ID, RAPID_SHOOT_MAXLVL)
		def..removeChannelProperties(true, true)..presetButtonPosNormal(1, 2)
		..setIconNormal("BTNPlasmaBeam")
		let tgen = new AbilityTooltipGenerator(RAPID_SHOOT_NAME, "W", Targettype.NOTARGET, RAPID_SHOOT_MAXLVL, "rapidz")
		tgen..addProperty("Duration:", (int lvl) -> getRapidShootDuration(lvl).toString())
		tgen..addProperty("Damage:", (int lvl) -> getRapidShootDamage(lvl).toString())
		..applyToDef(def)
		
		let def2 = new AbilityDefinitionCargoHoldBurrow(DISABLE_ATTACK_ID)
		def2..setName("disabled")..setCargoCapacity(1,0)
	